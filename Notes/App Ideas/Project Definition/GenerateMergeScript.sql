DECLARE @json NVARCHAR(MAX) = (
	SELECT * FROM [Core].[Users] FOR JSON AUTO
);

DECLARE @schema NVARCHAR(255) = 'Core',
		@table NVARCHAR(255) = 'Users';
--this is needed for the merge

WITH [Tables] AS(
	SELECT
		'[' + [schemas].[name] + '].[' + [tables].[name] + ']'  AS[TableName]
		, [tables].[object_id] AS[TableId]
		, [schemas].[name] AS[Schema]
		, [tables].[name] AS [Table]
	FROM [sys].[schemas]
	INNER JOIN [sys].[tables]
		ON [tables].[schema_id] = [schemas].[schema_id]
), [Columns] AS (
	SELECT 
		 [columns].[object_id]
		 ,[columns].[name] AS [ColumnName]
		 ,[columns].[column_id] AS [ColumnOrder]
		 --,'JSON_VALUE(@json, ''$[0].'+ [columns].[name] +''') AS ['+[columns].[name]+']' AS 
		 ,[types].[name] AS [Type]
		 ,[types].[name] + ISNULL('(' + CASE [types].[name] 
			WHEN 'binary' THEN CAST([columns].[max_length] AS NVARCHAR)
			WHEN 'char' THEN CAST([columns].[max_length] AS NVARCHAR)
			WHEN 'nchar' THEN CAST([columns].[max_length] AS NVARCHAR)
		
			WHEN 'time' THEN CAST([columns].[scale] AS NVARCHAR)
			WHEN 'datetime2' THEN CAST([columns].[scale] AS NVARCHAR)
			WHEN 'datetimeoffset' THEN CAST([columns].[scale] AS NVARCHAR)
		
			WHEN 'decimal' THEN CAST([columns].[precision] AS NVARCHAR) + ', ' + CAST([columns].[scale] AS NVARCHAR)
			WHEN 'numeric' THEN CAST([columns].[precision] AS NVARCHAR) + ', ' + CAST([columns].[scale] AS NVARCHAR)
		
			WHEN 'varbinary' THEN CASE [columns].[max_length]  WHEN -1 THEN 'MAX' ELSE CAST([columns].[max_length] AS NVARCHAR) END
			WHEN 'varchar' THEN CASE [columns].[max_length]  WHEN -1 THEN 'MAX' ELSE CAST([columns].[max_length] AS NVARCHAR) END
			WHEN 'nvarchar' THEN CASE [columns].[max_length]  WHEN -1 THEN 'MAX' ELSE CAST([columns].[max_length] AS NVARCHAR) END
		 END + ')','') AS [TypeDefinition]

	FROM [sys].[columns]
	INNER JOIN [sys].[types]
		ON [columns].[user_type_id] = [types].[user_type_id]
			AND [columns].[system_type_id] = [types].[system_type_id]
)

SELECT CAST(STRING_AGG(r.r, + CHAR(13) + CHAR(10)) AS XML)
FROM (VALUES
	('SELECT *'),
	('FROM OPENJSON(@json)'),
	('WITH ('),
	((SELECT 
		STRING_AGG(CHAR(9) + '[' + [Columns].[ColumnName] + '] ' + [Columns].[TypeDefinition] + ' ''$.' + [Columns].[ColumnName] + '''',',' + CHAR(13) + CHAR(10))
			WITHIN GROUP (ORDER BY [Columns].[ColumnOrder])
	FROM [Columns]
	INNER JOIN [Tables]
		ON [Columns].[object_id] = [Tables].[TableId]
	WHERE
		[Tables].[Schema] = @schema
		AND [Tables].[Table] = @table)),
	(')')
)AS r(r)
